#  GLPK model file created by SUN for RO teaching


###############################  Model ###############################



###############################  Sets  ###############################

set COMMANDE;
set PRODUIT;
set MAGASIN;

################### Variables ###################

var Q{i in COMMANDE, j in MAGASIN, k in PRODUIT}, >=0, integer; #Répartition des produits par commande et par magasin
var Cout{i in COMMANDE, j in MAGASIN}, binary; # Variable binaire pour le coût fixe
var Y{k in COMMANDE, i in MAGASIN} >= 0, integer; # Nombre de produit total par commande par magasin

###################  Constants: Data to load   #########################

param PC{i in COMMANDE, j in PRODUIT}; # Produits demandés par magasin
param CU{i in MAGASIN, j in PRODUIT}; # Coûts unitaire par magasin d'origine
param SP{i in MAGASIN, j in PRODUIT}; # Stock de produits par magasin
param CF{i in COMMANDE, j in MAGASIN}; # Coût fixe d'expédition par colis
param CV{i in COMMANDE, j in MAGASIN}; # Coût variable d'expédition par colis
param M; # M doit être plus grand que la somme du nombre de produit commandé

################### Constraints ###################

s.t. DefinitionDeY{i in COMMANDE, j in MAGASIN}:
    Y[i,j] = sum{k in PRODUIT} Q[i,j,k];

s.t. CommandeInferieurAuStock{j in MAGASIN, k in PRODUIT}:
	sum{i in COMMANDE} Q[i,j,k] <= SP[j,k];

s.t. LivraisonComplete{i in COMMANDE, k in PRODUIT}:
	sum{j in MAGASIN} Q[i,j,k] = PC[i,k];

s.t. MatriceDeCout{i in COMMANDE, j in MAGASIN}:
	Y[i,j] <= M*Cout[i,j];

s.t. MatriceDeCout2{i in COMMANDE, j in MAGASIN}:
	Y[i,j] >= -M*Cout[i,j];

###### Objective ######

minimize ImpactMinimal:
	# Suivant la compréhension de l'énoncé, on peut rajouter le coût unitaire ou non
	#(sum{i in COMMANDE, j in MAGASIN, k in PRODUIT} Q[i,j,k]*CU[j,k]) +
	  (sum{i in COMMANDE, j in MAGASIN} CF[i,j]*Cout[i,j])
	+ (sum{i in COMMANDE, j in MAGASIN} CV[i,j]*Y[i,j]);

#end;


#default data

data;

param M := 10;

set MAGASIN :=
M1
M2
M3;

set PRODUIT :=
P1
P2;

set COMMANDE :=
D1
D2;

param PC : P1 P2 :=
D1 2 0
D2 1 3;

param SP : P1 P2 :=
M1 2.5 1
M2 1 2
M3 2 1;

param CU: P1 P2 :=
M1 1 1
M2 2 3
M3 3 2;

param CF: M1 M2 M3 :=
D1 110 90 100
D2 110 90 100;

param CV: M1 M2 M3 :=
D1 10 1 5
D2 2 20 10;

end;